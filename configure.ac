dnl Process this file with autoconf to produce a configure script.

AC_INIT(gcompris, 9.6, http://gcompris.net)

AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)
AC_CONFIG_MACRO_DIR([m4])

if test -z "$enable_maintainer_mode"; then
  enable_maintainer_mode=yes
fi
AM_MAINTAINER_MODE([enable])

# Use the silent-rules feature when possible.
m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

dnl make sure we keep ACLOCAL_FLAGS around for maintainer builds to work
AC_SUBST(ACLOCAL_AMFLAGS, "\${ACLOCAL_FLAGS}")

dnl libtool versioning from libgnome

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AM_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
AM_PROG_CC_C_O

GNOME_COMPILE_WARNINGS(yes)

#*******************************************************************************
# Internationalization
#*******************************************************************************
GETTEXT_PACKAGE=gcompris
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [GETTEXT package name])

IT_PROG_INTLTOOL([0.35.0])
AM_GLIB_GNU_GETTEXT

#*******************************************************************************
# Check required GNOME libraries
#*******************************************************************************

GTK_REQUIRED=2.12.0
LIBRSVG_REQUIRED=2.26.0
LIBXML_REQUIRED=2.6.16

dnl
dnl Start of pkg-config checks
dnl

PKG_CHECK_MODULES(GCOMPRIS, \
  gtk+-2.0 >= $GTK_REQUIRED \
  librsvg-2.0 >= $LIBRSVG_REQUIRED \
  libxml-2.0 >= $LIBXML_REQUIRED \
  gthread-2.0 )

AC_SUBST(GCOMPRIS_CFLAGS)
AC_SUBST(GCOMPRIS_LIBS)

dnl OSX Specifics
AC_MSG_CHECKING([for OSX])
case "$host" in
  *-apple-darwin*)
    native_osx=yes
    OSX_CFLAGS="$CFLAGS -arch i386 "
    ;;
  *)
    AC_CHECK_LIB(X11, XOpenDisplay)
    native_osx=no
    OSX_CFLAGS=""
    ;;
esac
AC_MSG_RESULT([$native_osx])

AM_CONDITIONAL(OS_MACOSX, test "$native_osx" = yes)

# nsbundle default is no even for osx
nsbundle="no"

AC_ARG_ENABLE(nsbundle,
          AC_HELP_STRING(
          [--enable-nsbundle],
          [Turn on nsbundle packaging (needs Cocoa/GNUstep)]),
          nsbundle="$enableval", nsbundle="no")

#check header for nsbundle
if test "x$nsbundle" = "xyes"; then
   AC_PROG_OBJC([gcc])
   AC_PROG_OBJCPP([gcc])

   if test "x$native_osx" = "xyes"; then
      AC_LANG_PUSH([Objective C])
      AC_CHECK_HEADER(Foundation/Foundation.h, NSFoundation="yes" , NSFoundation="no")
      if test "x$NSFoundation" = "x$no"; then
	 AC_MSG_ERROR([You need development tools to use nsbundle. Make sure XCode is installed." ])
      fi
      OBJCFLAGS="$OBJCFLAGS $CFLAGS"
      AC_LANG_POP([Objective C])

   else
      AC_LANG_PUSH([Objective C])
      saved_CPPFLAGS="$CPPFLAGS"
      if test "x$GNUSTEP_SYSTEM_ROOT" != "x"; then
         CPPFLAGS="$CPPFLAGS -I$GNUSTEP_SYSTEM_ROOT/Library/Headers "
      fi
      AC_CHECK_HEADER(Foundation/Foundation.h, NSFoundation="yes" , NSFoundation="no")
      if test "x$NSFoundation" = "xno"; then
         AC_MSG_ERROR([You need GNUstep dev to use nsbundle. Make sure GNUSTEP_SYSTEM_ROOT is set." ])
      fi
      CPPFLAGS="$saved_CPPFLAGS"
      OBJCFLAGS="$OBJCFLAGS $CFLAGS -DGNUSTEP -DGNUSTEP_BASE_LIBRARY=1 -DGNU_GUI_LIBRARY=1 -DGNU_RUNTIME=1 -DGNUSTEP_BASE_LIBRARY=1 -fconstant-string-class=NSConstantString -I$GNUSTEP_SYSTEM_ROOT/Library/Headers "
      AC_LANG_POP([Objective C])
   fi
else
  OBJC=$CC
  AC_SUBST(OBJC)
fi

_AM_DEPENDENCIES([OBJC])

### --------------------------------------------------------------------------
### Check to see if GDK uses the quartz backend and if we can use
### MacOSX integration
_gdk_tgt=`$PKG_CONFIG --variable=target gdk-2.0`
AM_CONDITIONAL([GDK_TARGET_QUARTZ], [test x$_gdk_tgt = xquartz])
if test "x$_gdk_tgt" = xquartz; then
   PKG_CHECK_MODULES(IGE_MAC, ige-mac-integration)
   AC_SUBST(IGE_MAC_LIBS)
   AC_SUBST(IGE_MAC_CFLAGS)
   AC_DEFINE_UNQUOTED(MAC_INTEGRATION, 1, [Integrate in the OSX Menu])
fi

# Provide relocatable API from autopackage
# From http://autopackage.org/docs/binreloc/
enable_binreloc=auto
if test x"$nsbundle" = "xyes" ; then
enable_binreloc=yes
AC_MSG_NOTICE([binreloc enabled by nsbundle])
fi

AM_BINRELOC

dnl glib-genmarshal
GLIB_GENMARSHAL=`$PKG_CONFIG --variable=glib_genmarshal glib-2.0`
AC_SUBST(GLIB_GENMARSHAL)

GLIB_MKENUMS=`$PKG_CONFIG --variable=glib_mkenums glib-2.0`
AC_SUBST(GLIB_MKENUMS)

AC_ARG_ENABLE(rebuilds,
              [AC_HELP_STRING([--disable-rebuilds],
                              [disable all source autogeneration rules])],,
              [enable_rebuilds=yes])

# define a MAINT-like variable REBUILD which is set if Perl
# and awk are found, so autogenerated sources can be rebuilt
AC_PATH_PROGS(PERL, perl5 perl)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

LDFLAGS="${LDFLAGS}"

dnl Add the languages which your application supports here.
ALL_LINGUAS="af am ar ast az bg br ca cs da de dz el en_CA en_GB eo es et eu fa fi fr ga gl gu he hi hr hu id it ja ka ko lt mk ml mr ms nb ne nl nn oc pa pl pt pt_BR ro ru rw sk sl so sq sr sr_ME sr@Latn sv ta th tr uk ur vi wa zh_CN zh_HK zh_TW zu"

dnl GCompris needs to know which locale are supported
AC_SUBST(ALL_LINGUAS)
AC_DEFINE_UNQUOTED(ALL_LINGUAS, "${ALL_LINGUAS}", [Supported languages])

BUILD_DATE=`date +%y%m`
AC_DEFINE_UNQUOTED(BUILD_DATE,"$BUILD_DATE", [Date at which GCompris has been built])
AC_SUBST(BUILD_DATE)

dnl Define myprefix depending on wether our user gives us one or not
if test "x${prefix}" = "xNONE"; then
  myprefix=${ac_default_prefix}
else
  myprefix=${prefix}
fi

dnl Define myprefix depending on wether our user gives us one or not
dnl This is needed for $libdir !
if test "x${exec_prefix}" = "xNONE"; then
  exec_prefix=${myprefix}
fi



dnl Some debug option
AC_ARG_ENABLE(debug,
          AC_HELP_STRING(
          [--enable-debug],
          [Turn on debugging messages]),
          USE_DEBUG="$enableval", USE_DEBUG="no")
if test "x$USE_DEBUG" == "xyes" ; then
    AC_DEFINE_UNQUOTED(DEBUG, 1, "Enable debug messages.")
    CFLAGS="$CFLAGS $OSX_CFLAGS -Wall -Werror -g"
else
    CFLAGS="$CFLAGS $OSX_CFLAGS -Wall -Werror -O2"
fi

dnl disable activation code
AC_ARG_ENABLE(disable-activation-code,
          AC_HELP_STRING(
          [--disable-activation-code],
          [For Windows or MacOSX version, disable the activation code]),
          disable_activation_code="$enableval", disable_activation_code="no")

dnl WIN32 Specifics
AC_MSG_CHECKING([for Win32 platform in general])
case "$host" in
  *-*-mingw*|*-*-cygwin*)
    platform_win32=yes
    ;;
  *)
    platform_win32=no
    ;;
esac
AC_MSG_RESULT($platform_win32)
AM_CONDITIONAL(PLATFORM_WIN32, test "$platform_win32" = yes)
AM_CONDITIONAL(BUILD_MINGW32, test "$platform_win32" = yes)
if test x"$platform_win32" = xyes; then
   LIBS="$LIBS -luuid -lole32 -lwsock32 -mno-cygwin -mms-bitfields -mwindows -mconsole"
   #CFLAGS="$CFLAGS -D__GW32__ -DWIN32 -I$CROSS_ROOT/include -I$CROSS_ROOT/include/glibc"
   #CFLAGS="$CFLAGS -DWIN32"
fi


# Ensure MSVC-compatible struct packing convention is used when
# compiling for Win32 with gcc. GTK+ uses this convention, so we must, too.
# What flag to depends on gcc version: gcc3 uses "-mms-bitfields", while
# gcc2 uses "-fnative-struct".
if test x"$native_win32" = xyes; then
  if test x"$GCC" = xyes; then
    msnative_struct=''
    AC_MSG_CHECKING([how to get MSVC-compatible struct packing])
    if test -z "$ac_cv_prog_CC"; then
      our_gcc="$CC"
    else
      our_gcc="$ac_cv_prog_CC"
    fi
    case `$our_gcc --version | sed -e 's,\..*,.,' -e q` in
      2.)
	if $our_gcc -v --help 2>/dev/null | grep fnative-struct >/dev/null; then
	  msnative_struct='-fnative-struct'
	fi
	;;
      *)
	if $our_gcc -v --help 2>/dev/null | grep ms-bitfields >/dev/null; then
	  msnative_struct='-mms-bitfields'
	fi
	;;
    esac
    if test x"$msnative_struct" = x ; then
      AC_MSG_RESULT([no way])
      AC_MSG_WARN([produced libraries will be incompatible with prebuilt GTK+ DLLs])
    else
      CFLAGS="$CFLAGS $msnative_struct"
      AC_MSG_RESULT([${msnative_struct}])
    fi
  fi
fi

dnl
dnl === DIRECTORY DEFINITIONS ===
dnl

if test "x$platform_win32" = "xyes" ; then
  DATADIRNAME="share"

  dnl Set PACKAGE_LOCALE_DIR in config.h for WINDOWS
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "../${DATADIRNAME}/locale", [GCompris locale directory])

  dnl Set PACKAGE_DATA_DIR
  PACKAGE_DATA_DIR="boards"
  AC_SUBST(PACKAGE_DATA_DIR)
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "../${DATADIRNAME}", [GCompris data directory])

  dnl Plugin Directory
  AC_DEFINE_UNQUOTED(PACKAGE_CLIB_DIR, "../${DATADIRNAME}", [Gcompris plugins directory])
  plugindir=${myprefix}/${DATADIRNAME}/gcompris
  AC_SUBST(plugindir)

  dnl Python plugin Directory
  PYTHON_PLUGIN_DIR="${myprefix}/${DATADIRNAME}/gcompris/python"
  AC_SUBST(PYTHON_PLUGIN_DIR)

  dnl System GCompris config directory
  AC_DEFINE_UNQUOTED(SYSTEM_CONFIG_DIR, "etc", [System GCompris config directory])

  dnl Test for gnuchess
  AC_DEFINE_UNQUOTED(GNUCHESS, "gnuchess.exe", Defines where GNU Chess resides on the system)
  build_gnuchess=yes

  if test x$disable_activation_code = xno; then
    AC_DEFINE([ACTIVATION_CODE], 1,[Activation code is enabled])
  fi

elif test "x$nsbundle" = "xyes" ; then
  DATADIRNAME="share"

  dnl Set PACKAGE_LOCALE_DIR in config.h for MacOSX
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "../Resources/${DATADIRNAME}/locale", [GCompris locale directory])

  dnl Set PACKAGE_DATA_DIR
  PACKAGE_DATA_DIR="boards"
  AC_SUBST(PACKAGE_DATA_DIR)
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "../Resources/${DATADIRNAME}", [GCompris data directory])

  dnl Plugin Directory
  AC_DEFINE_UNQUOTED(PACKAGE_CLIB_DIR, "../Resources/lib", [Gcompris plugins directory])
  plugindir=${myprefix}/lib/gcompris
  AC_SUBST(plugindir)

  dnl Python plugin Directory
  PYTHON_PLUGIN_DIR=${myprefix}/${DATADIRNAME}/gcompris/python
  AC_SUBST(PYTHON_PLUGIN_DIR)

  dnl System GCompris config directory
  AC_DEFINE_UNQUOTED(SYSTEM_CONFIG_DIR, "../Resources/etc", [System GCompris config directory])

  dnl Test for gnuchess
  AC_DEFINE_UNQUOTED(GNUCHESS, "../Resources/bin/gcompris-gnuchess", Defines where GNU Chess resides on the system)
  build_gnuchess=yes

  if test x$disable_activation_code = xno; then
    AC_DEFINE([ACTIVATION_CODE], 1,[Activation code is enabled])
  fi
else
  dnl Set PACKAGE_LOCALE_DIR in config.h.
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${myprefix}/${DATADIRNAME}/locale", [GCompris locale directory])

  dnl Set PACKAGE_DATA_DIR
  PACKAGE_DATA_DIR="boards"
  AC_SUBST(PACKAGE_DATA_DIR)
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${myprefix}/${DATADIRNAME}", [GCompris data directory])

  dnl Plugin Directory
  my_libdir=`eval echo "${libdir}"`
  AC_DEFINE_UNQUOTED(PACKAGE_CLIB_DIR, "${my_libdir}", [Gcompris plugins directory])
  plugindir=$libdir/gcompris
  AC_SUBST(plugindir)

  dnl Python plugin Directory
  PYTHON_PLUGIN_DIR="${myprefix}/${DATADIRNAME}/${PACKAGE}/python"
  AC_SUBST(PYTHON_PLUGIN_DIR)

  dnl System GCompris config directory
  AC_DEFINE_UNQUOTED(SYSTEM_CONFIG_DIR, "/etc", [System GCompris config directory])

  dnl Test for gnuchess
  AC_PATH_PROGS(GNUCHESS, gnuchess gnome-gnuchess, no, [/usr/bin:/usr/games:/usr/local/bin:$PATH])
  if test x$GNUCHESS = xno; then
    AC_MSG_ERROR(Couldn't find gnuchess, please install the gnuchess package version 5 or above)
  fi
  AC_DEFINE_UNQUOTED(GNUCHESS, "$GNUCHESS", Defines where GNU Chess resides on the system)
  build_gnuchess=no
fi
AM_CONDITIONAL(BUILD_GNUCHESS, test x$build_gnuchess != xno)

AC_PATH_PROG(TEXINFO, makeinfo,no)
if test x$TEXINFO = xno; then
  AC_MSG_WARN(Couldn't find texinfo, docs are not compiled. please install the texinfo package)
fi
AM_CONDITIONAL(TEXINFO, test x$TEXINFO != xno)

AC_PATH_PROG(TETEX, texi2html,no)
if test x$TETEX = xno; then
  AC_MSG_WARN(Couldn't find texi2html usualy in the tetex package, please install it)
fi


dnl SDLMixer support
AC_MSG_CHECKING([wether we build with SDLMixer (will replace gstreamer by sdl mixer if enabled)])
AC_ARG_ENABLE(sdlmixer,
          AC_HELP_STRING(
          [--enable-sdlmixer],
          [Turn on sdl mixer (will replace gstreamer by sdl mixer)]),
          with_sdlmixer="$enableval", with_sdlmixer="no")
AC_MSG_RESULT($with_sdlmixer)

if test x$with_sdlmixer = xyes; then
  PKG_CHECK_MODULES(AUDIO, sdl,, AC_MSG_ERROR([*** SDL Mixer not found!]))
  AC_DEFINE([USE_SDLMIXER], 1,[sdl mixer is enabled])
  AUDIO_LIBS="$AUDIO_LIBS -lSDL_mixer"
else
  dnl Default is gstreamer
  PKG_CHECK_MODULES(AUDIO, gstreamer-0.10,, AC_MSG_ERROR([*** GSTREAMER not found!]))
  AC_DEFINE([USE_GSTREAMER], 1,[gstreamer is enabled])
fi
AC_SUBST(AUDIO_CFLAGS)
AC_SUBST(AUDIO_LIBS)

dnl GNET support
AC_MSG_CHECKING([wether we build with GNET (if not, networking will be disabled)])
AC_ARG_ENABLE(gnet,
          AC_HELP_STRING(
          [--disable-gnet],
          [Turn off gnet (will not let GCompris fetch content from a web server)]),
          with_gnet="$enableval", with_gnet="yes")
AC_MSG_RESULT($with_gnet)

if test x$with_gnet = xyes; then
  PKG_CHECK_MODULES(GNET, gnet-2.0,, AC_MSG_ERROR([*** GNET not found!]))
  AC_DEFINE([USE_GNET], 1,[Networking is enabled])
fi

dnl SQLITE support
AC_MSG_CHECKING([wether we build with SQLITE (if not profile will be disabled)])
AC_ARG_ENABLE(sqlite,
          AC_HELP_STRING(
          [--disable-sqlite],
          [Turn off sqlite (will disable profile)]),
          with_sqlite="$enableval", with_sqlite="yes")
AC_MSG_RESULT($with_sqlite)

dnl DBUS support (Needed only for OLPC XO)
AC_MSG_CHECKING([wether we build with DBUS (needed only for OLPC SUGAR XO)])
AC_ARG_ENABLE(dbus,
          AC_HELP_STRING(
          [--enable-dbus],
          [Turn on dbus (will let GCompris integrate well on OLPC SUGAR XO)]),
          with_dbus="$enableval", with_dbus="no")
AC_MSG_RESULT($with_dbus)

if test x$with_dbus = xyes; then
  PKG_CHECK_MODULES(DBUS, [dbus-1 dbus-glib-1],, AC_MSG_ERROR([*** DBUS not found!]))
  AC_DEFINE([USE_DBUS], 1,[DBUS support is enabled])
fi

dnl Sugar DE support
AC_MSG_CHECKING([wether we build with Sugar DE support])
AC_ARG_ENABLE(sugar,
          AC_HELP_STRING(
          [--enable-sugar],
          [Turn on sugar support (will let GCompris integrate well on Sugar DE)]),
          with_sugar="$enableval", with_sugar="no")
AC_MSG_RESULT($with_sugar)
if test x$with_sugar = xyes; then
  if test x$with_dbus = xno; then
    AC_MSG_ERROR([You need to add DBUS support as well.])
  fi
  PKG_CHECK_MODULES(SUGAR, [sugar-toolkit])
  AC_DEFINE([USE_SUGAR], 1, [Sugar DE support is enabled])
fi
AM_CONDITIONAL([SUGAR], [test x$with_sugar = xyes])

dnl check for python
AC_ARG_WITH(python,AC_HELP_STRING(
    [--with-python=path],
    [set the path for python]), python_path=$withval, python_path=not)
if test x$python_path = xno ; then
build_python_plugin="no"
else


if test x$python_path != xnot ; then
PYTHON=$python_path
fi

AM_PATH_PYTHON
AM_CHECK_PYTHON_HEADERS( [build_python_plugin="yes"], [build_python_plugin="no"] )

fi

dnl Finally output the required python variables and automake defines.
AM_CONDITIONAL(PYTHON_PLUGIN, test x$build_python_plugin = xyes)

dnl build only support
AC_ARG_ENABLE(py-build-only,
          AC_HELP_STRING(
          [--enable-py-build-only],
          [Skip python modules tests. (Useful if you just need build)]),
          py_build_only="$enableval", py_build_only="no")

REQUIRE_PYTHON=""
if test x$build_python_plugin = xno; then
  AC_MSG_NOTICE([Python plugin is disabled.])
else
  dnl Python interpreter is available so check for pygtk
  PKG_CHECK_MODULES(PYGTK, pygtk-2.0 pycairo)

  PYCAIRO_VERSION=$(pkg-config pycairo --modversion | awk -F. '{print ($1 * 1000) + $2 }')
  AC_DEFINE_UNQUOTED([PYCAIRO_VERSION], $PYCAIRO_VERSION, [pycairo version])

  AC_MSG_CHECKING([for pygtk defs])
  PYGTK_DEFSDIR=`$PKG_CONFIG --variable=defsdir pygtk-2.0`
  AC_MSG_RESULT([$PYGTK_DEFSDIR])

  AC_MSG_CHECKING([for pygtk codegen])
  PYGTK_CODEGEN="$PYTHON `$PKG_CONFIG --variable=codegendir pygtk-2.0`/codegen.py"
  AC_MSG_RESULT([$PYGTK_CODEGEN])

  AC_MSG_CHECKING([for pygtk h2def])
  PYGTK_H2DEF="$PYTHON `$PKG_CONFIG --variable=codegendir pygtk-2.0`/h2def.py"
  AC_MSG_RESULT([$PYGTK_H2DEF])

  AC_SUBST([PYGTK_DEFSDIR])
  AC_SUBST([PYGTK_CODEGEN])
  AC_SUBST([PYGTK_H2DEF])

  if test x$py_build_only = xno; then
    AM_CHECK_PYMOD(gtk,,,AC_MSG_ERROR([*** pygtk installed but not visible from python ]))

    dnl Set this variable for the .spec.in file
    REQUIRE_PYTHON="python pygtk2.0"

    if test x$with_sqlite = xyes; then
        if test x$PYTHON_VERSION = x2.4; then
	    AM_CHECK_PYMOD(pysqlite2,,,AC_MSG_ERROR([*** pysqlite2 missing ]))
	    REQUIRE_PYTHON="$REQUIRE_PYTHON python-sqlite2"
        fi
    fi

  else
    AC_MSG_WARN([py-build-only: Skipping python modules dependancies])
    AC_MSG_WARN([py-build-only: Do not forget to install pygtk, pysqlite2])
  fi

fi

if test x$with_sqlite = xyes; then
    PKG_CHECK_MODULES(SQLITE3, sqlite3,, AC_MSG_ERROR([*** SQLITE3 not found!]))
    AC_DEFINE([USE_SQLITE], 1,[SQLITE Database, Profiles are enabled])
fi

AM_CONDITIONAL(USE_SQLITE, test x$with_sqlite = xyes)
AC_SUBST(SQLITE3_CFLAGS)
AC_SUBST(SQLITE3_LIBS)

AC_SUBST(REQUIRE_PYTHON)

dnl mingw32 specific stuff.
dnl use libgw32c
dnl define __GW32__ and WIN32

# Check platform - see if WinMain needed:
AC_SUBST(CFLAGS)
AC_SUBST(LIBS)

dnl Autoconf output
AC_OUTPUT([
Makefile.mingw
Makefile
autopackage/Makefile
autopackage/default.apspec
boards/Makefile
config.h.mingw
docs/C/Makefile
docs/Makefile
docs/eu/Makefile
docs/fr/Makefile
gcompris-installer.nsi
macosx/Info.plist
macosx/InfoAdmin.plist
po/Makefile.in
src/Makefile
src/administration-activity/Makefile
src/administration-activity/admin/Makefile
src/administration-activity/resources/Makefile
src/administration-activity/resources/administration/Makefile
src/advanced_colors-activity/Makefile
src/advanced_colors-activity/resources/Makefile
src/advanced_colors-activity/resources/advanced_colors/Makefile
src/algebra_by-activity/Makefile
src/algebra_by-activity/resources/Makefile
src/algebra_by-activity/resources/algebra/Makefile
src/algebra_guesscount-activity/Makefile
src/algebra_guesscount-activity/resources/Makefile
src/algebra_guesscount-activity/resources/algebra_guesscount/Makefile
src/algebra_minus-activity/Makefile
src/algebra_plus-activity/Makefile
src/algorithm-activity/Makefile
src/algorithm-activity/resources/Makefile
src/algorithm-activity/resources/algorithm/Makefile
src/anim-activity/Makefile
src/anim-activity/resources/Makefile
src/anim-activity/resources/anim/Makefile
src/awele-activity/Makefile
src/awele-activity/resources/Makefile
src/awele-activity/resources/awele/Makefile
src/babymatch-activity/Makefile
src/babymatch-activity/resources/Makefile
src/babymatch-activity/resources/babymatch/Makefile
src/babymatch-activity/resources/shapegame/Makefile
src/babyshapes-activity/Makefile
src/babyshapes-activity/resources/Makefile
src/babyshapes-activity/resources/babyshapes/Makefile
src/babyshapes-activity/resources/babyshapes/dog1/Makefile
src/babyshapes-activity/resources/babyshapes/dog2/Makefile
src/babyshapes-activity/resources/babyshapes/dog3/Makefile
src/babyshapes-activity/resources/babyshapes/food/Makefile
src/ballcatch-activity/Makefile
src/ballcatch-activity/resources/Makefile
src/ballcatch-activity/resources/ballcatch/Makefile
src/bargame-activity/Makefile
src/bargame-activity/resources/Makefile
src/bargame-activity/resources/bargame/Makefile
src/billard-activity/Makefile
src/billard-activity/resources/Makefile
src/billard-activity/resources/billard/Makefile
src/boards/Makefile
src/boards/python/Makefile
src/boards/python/gcompris/Makefile
src/boards/python/gcompris/admin/Makefile
src/boards/python/gcompris/anim/Makefile
src/boards/python/gcompris/bonus/Makefile
src/boards/python/gcompris/score/Makefile
src/boards/python/gcompris/skin/Makefile
src/boards/python/gcompris/sound/Makefile
src/boards/python/gcompris/timer/Makefile
src/boards/python/gcompris/utils/Makefile
src/canal_lock-activity/Makefile
src/canal_lock-activity/resources/Makefile
src/canal_lock-activity/resources/canal_lock/Makefile
src/chat-activity/Makefile
src/chat-activity/resources/Makefile
src/chat-activity/resources/chat/Makefile
src/chess_computer-activity/Makefile
src/chess_computer-activity/resources/Makefile
src/chess_computer-activity/resources/chess/Makefile
src/chess_computer-activity/gnuchess/Makefile
src/chess_movelearn-activity/Makefile
src/chess_partyend-activity/Makefile
src/chronos-activity/Makefile
src/chronos-activity/resources/Makefile
src/chronos-activity/resources/chronos/Makefile
src/click_on_letter-activity/Makefile
src/click_on_letter-activity/resources/Makefile
src/click_on_letter-activity/resources/click_on_letter/Makefile
src/clickanddraw-activity/Makefile
src/clickgame-activity/Makefile
src/clickgame-activity/resources/Makefile
src/clickgame-activity/resources/clickgame/Makefile
src/clockgame-activity/Makefile
src/clockgame-activity/resources/Makefile
src/clockgame-activity/resources/clockgame/Makefile
src/colors-activity/Makefile
src/colors-activity/resources/Makefile
src/colors-activity/resources/colors/Makefile
src/connect4-2players-activity/Makefile
src/connect4-activity/Makefile
src/connect4-activity/connect4p/Makefile
src/connect4-activity/resources/Makefile
src/connect4-activity/resources/connect4/Makefile
src/crane-activity/Makefile
src/crane-activity/resources/Makefile
src/crane-activity/resources/crane/Makefile
src/details-activity/Makefile
src/details-activity/resources/Makefile
src/details-activity/resources/details/Makefile
src/doubleentry-activity/Makefile
src/doubleentry-activity/resources/Makefile
src/doubleentry-activity/resources/doubleentry/Makefile
src/draw-activity/Makefile
src/drawnumber-activity/Makefile
src/drawnumber-activity/resources/Makefile
src/drawnumber-activity/resources/drawnumber/Makefile
src/electric-activity/Makefile
src/electric-activity/resources/Makefile
src/electric-activity/resources/electric/Makefile
src/enumerate-activity/Makefile
src/enumerate-activity/resources/Makefile
src/enumerate-activity/resources/enumerate/Makefile
src/erase-activity/Makefile
src/erase-activity/resources/Makefile
src/erase-activity/resources/erase/Makefile
src/erase_clic-activity/Makefile
src/erase_2clic-activity/Makefile
src/fifteen-activity/Makefile
src/fifteen-activity/resources/Makefile
src/fifteen-activity/resources/fifteen/Makefile
src/findit-activity/Makefile
src/followline-activity/Makefile
src/followline-activity/resources/Makefile
src/followline-activity/resources/followline/Makefile
src/gcompris/Makefile
src/geo_country-activity/Makefile
src/geo_country-activity/resources/Makefile
src/geo_country-activity/resources/geo_country/Makefile
src/geo_country-activity/resources/geo_country/argentina/Makefile
src/geo_country-activity/resources/geo_country/brazil/Makefile
src/geo_country-activity/resources/geo_country/canada/Makefile
src/geo_country-activity/resources/geo_country/france/Makefile
src/geo_country-activity/resources/geo_country/germany/Makefile
src/geo_country-activity/resources/geo_country/italy/Makefile
src/geo_country-activity/resources/geo_country/mexico/Makefile
src/geo_country-activity/resources/geo_country/norway/Makefile
src/geo_country-activity/resources/geo_country/poland/Makefile
src/geo_country-activity/resources/geo_country/turkey/Makefile
src/geo_country-activity/resources/geo_country/usa/Makefile
src/geography-activity/Makefile
src/geography-activity/resources/Makefile
src/geography-activity/resources/geography/Makefile
src/geography-activity/resources/geography/africa/Makefile
src/geography-activity/resources/geography/afrique/Makefile
src/geography-activity/resources/geography/continents/Makefile
src/geography-activity/resources/geography/europe/Makefile
src/geography-activity/resources/geography/northamerica/Makefile
src/geography-activity/resources/geography/southamerica/Makefile
src/gletters-activity/Makefile
src/gletters-activity/resources/Makefile
src/gletters-activity/resources/gletters/Makefile
src/gnumch-equality-activity/Makefile
src/gnumch-equality-activity/resources/Makefile
src/gnumch-equality-activity/resources/gnumch/Makefile
src/gnumch-factors-activity/Makefile
src/gnumch-inequality-activity/Makefile
src/gnumch-multiples-activity/Makefile
src/gnumch-primes-activity/Makefile
src/goocanvas/Makefile
src/goocanvas/src/Makefile
src/guessnumber-activity/Makefile
src/guessnumber-activity/resources/Makefile
src/guessnumber-activity/resources/guessnumber/Makefile
src/hangman-activity/Makefile
src/hangman-activity/resources/Makefile
src/hangman-activity/resources/hangman/Makefile
src/hanoi-activity/Makefile
src/hanoi_real-activity/Makefile
src/hanoi_real-activity/resources/Makefile
src/hanoi_real-activity/resources/hanoi_real/Makefile
src/hexagon-activity/Makefile
src/hexagon-activity/resources/Makefile
src/hexagon-activity/resources/hexagon/Makefile
src/hydroelectric-activity/Makefile
src/hydroelectric-activity/resources/Makefile
src/hydroelectric-activity/resources/hydroelectric/Makefile
src/imageid-activity/Makefile
src/imageid-activity/resources/Makefile
src/imageid-activity/resources/imageid/Makefile
src/imagename-activity/Makefile
src/imagename-activity/resources/Makefile
src/imagename-activity/resources/imagename/Makefile
src/instruments-activity/Makefile
src/instruments-activity/resources/Makefile
src/instruments-activity/resources/instruments/Makefile
src/leftright-activity/Makefile
src/leftright-activity/resources/Makefile
src/leftright-activity/resources/leftright/Makefile
src/lightsoff-activity/Makefile
src/lightsoff-activity/resources/Makefile
src/lightsoff-activity/resources/lightsoff/Makefile
src/login-activity/Makefile
src/login-activity/resources/Makefile
src/login-activity/resources/login/Makefile
src/magic_hat_minus-activity/Makefile
src/magic_hat_minus-activity/resources/Makefile
src/magic_hat_minus-activity/resources/magic_hat/Makefile
src/magic_hat_plus-activity/Makefile
src/maze-activity/Makefile
src/maze-activity/resources/Makefile
src/maze-activity/resources/maze/Makefile
src/maze2DRelative-activity/Makefile
src/maze3D-activity/Makefile
src/mazeInvisible-activity/Makefile
src/melody-activity/Makefile
src/melody-activity/resources/Makefile
src/melody-activity/resources/melody/Makefile
src/memory-activity/Makefile
src/memory-activity/resources/Makefile
src/memory-activity/resources/memory/Makefile
src/memory_add-activity/Makefile
src/memory_add_minus-activity/Makefile
src/memory_add_minus_mult_div-activity/Makefile
src/memory_add_minus_mult_div_tux-activity/Makefile
src/memory_add_minus_tux-activity/Makefile
src/memory_add_tux-activity/Makefile
src/memory_div-activity/Makefile
src/memory_div_tux-activity/Makefile
src/memory_enumerate-activity/Makefile
src/memory_minus-activity/Makefile
src/memory_minus_tux-activity/Makefile
src/memory_mult-activity/Makefile
src/memory_mult_div-activity/Makefile
src/memory_mult_div_tux-activity/Makefile
src/memory_mult_tux-activity/Makefile
src/memory_sound-activity/Makefile
src/memory_sound_tux-activity/Makefile
src/memory_tux-activity/Makefile
src/memory_wordnumber-activity/Makefile
src/missing_letter-activity/Makefile
src/missing_letter-activity/resources/Makefile
src/missing_letter-activity/resources/missing_letter/Makefile
src/money-activity/Makefile
src/money-activity/resources/Makefile
src/money-activity/resources/money/Makefile
src/money_cents-activity/Makefile
src/money_back-activity/Makefile
src/money_back_cents-activity/Makefile
src/mosaic-activity/Makefile
src/mosaic-activity/resources/Makefile
src/mosaic-activity/resources/mosaic/Makefile
src/paintings-activity/Makefile
src/paintings-activity/resources/Makefile
src/paintings-activity/resources/paintings/Makefile
src/paratrooper-activity/Makefile
src/paratrooper-activity/resources/Makefile
src/paratrooper-activity/resources/paratrooper/Makefile
src/penalty-activity/Makefile
src/penalty-activity/resources/Makefile
src/penalty-activity/resources/penalty/Makefile
src/photohunter-activity/Makefile
src/photohunter-activity/resources/Makefile
src/photohunter-activity/resources/photohunter/Makefile
src/planegame-activity/Makefile
src/planegame-activity/resources/Makefile
src/planegame-activity/resources/planegame/Makefile
src/pythontemplate-activity/Makefile
src/pythontest-activity/Makefile
src/railroad-activity/Makefile
src/railroad-activity/resources/Makefile
src/railroad-activity/resources/railroad/Makefile
src/readingh-activity/Makefile
src/readingh-activity/resources/Makefile
src/readingh-activity/resources/readingh/Makefile
src/readingh-activity/resources/wordsgame/Makefile
src/readingv-activity/Makefile
src/redraw-activity/Makefile
src/redraw-activity/resources/Makefile
src/redraw-activity/resources/redraw/Makefile
src/redraw_symmetrical-activity/Makefile
src/redraw_symmetrical-activity/resources/Makefile
src/redraw_symmetrical-activity/resources/redraw_symmetrical/Makefile
src/reversecount-activity/Makefile
src/reversecount-activity/resources/Makefile
src/reversecount-activity/resources/reversecount/Makefile
src/scalesboard-activity/Makefile
src/scalesboard-activity/resources/Makefile
src/scalesboard-activity/resources/scale/Makefile
src/scalesboard_weight-activity/Makefile
src/searace-activity/Makefile
src/searace-activity/resources/Makefile
src/searace-activity/resources/searace/Makefile
src/searace1player-activity/Makefile
src/smallnumbers-activity/Makefile
src/smallnumbers-activity/resources/Makefile
src/smallnumbers-activity/resources/smallnumbers/Makefile
src/smallnumbers2-activity/Makefile
src/submarine-activity/Makefile
src/submarine-activity/resources/Makefile
src/submarine-activity/resources/submarine/Makefile
src/sudoku-activity/Makefile
src/sudoku-activity/resources/Makefile
src/sudoku-activity/resources/sudoku/Makefile
src/superbrain-activity/Makefile
src/superbrain-activity/resources/Makefile
src/superbrain-activity/resources/superbrain/Makefile
src/tangram-activity/Makefile
src/tangram-activity/resources/Makefile
src/tangram-activity/resources/tangram/Makefile
src/target-activity/Makefile
src/target-activity/resources/Makefile
src/target-activity/resources/target/Makefile
src/traffic-activity/Makefile
src/traffic-activity/resources/Makefile
src/traffic-activity/resources/traffic/Makefile
src/tuxpaint-activity/Makefile
src/watercycle-activity/Makefile
src/watercycle-activity/resources/Makefile
src/watercycle-activity/resources/watercycle/Makefile
src/wordprocessor-activity/Makefile
src/wordprocessor-activity/resources/Makefile
src/wordprocessor-activity/resources/wordprocessor/Makefile
src/wordsgame-activity/Makefile
])

echo ""
echo "***************************************************"
echo ""
echo configure complete,
echo with options:

echo "Force python (--enable-py-build-only)    = $py_build_only"
echo "Python plugin                            = $build_python_plugin"

echo "SQLITE database (--enable-sqlite)        = $with_sqlite (profiles depend on this)"

echo "DBUS Support (OLPC XO Sugar)             = $with_dbus"
echo "Sugar DE Support                         = $with_sugar"
echo "GNET Networking (--enable-gnet)          = $with_gnet (networking depends on this)"
echo "BINRELOC (--enable-binreloc)             = $br_cv_binreloc"
echo "NSBundle (--enable-nsbundle)             = $nsbundle"
echo "SDL Mixer (--enable-sdlmixer)            = $with_sdlmixer (default is gstreamer)"


echo
echo gcompris will be installed in ${prefix}
echo to compile and install in in another directory
echo type sh configure --prefix=/usr
echo
echo type \'make\' to compile gcompris
echo type \'make install\' to install it
